plugins {
    id 'java'
    id 'maven-publish'
}

subprojects { subproject ->
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'cloud.grabsky'
    version = '0.9.7-SNAPSHOT'

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    final isSnapshot = version.endsWith('-SNAPSHOT')
    final hash7 = 'git rev-parse --verify --short HEAD'.execute().text.trim()

    publishing {
        repositories {
            maven {
                name 'Cloud'
                url (isSnapshot == true ? 'https://repo.grabsky.cloud/snapshots' : 'https://repo.grabsky.cloud/releases')
                credentials {
                    username System.getenv('REPO_USERNAME')
                    password System.getenv('REPO_SECRET')
                }
                authentication { basic(BasicAuthentication) }
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId rootProject.name + '-' + subproject.name
                version (isSnapshot == true ? hash7 : version)
                from components.java
            }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        (options as CoreJavadocOptions).addStringOption('Xdoclint:none', '-quiet')
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // projectlombok/lombok
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'
        // JetBrains/java-annotations
        compileOnly("org.jetbrains:annotations:23.0.0")
    }

}
