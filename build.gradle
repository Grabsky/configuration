plugins {
    id("java")
    id("maven-publish")
}

group = "cloud.grabsky"
version = "1.20.1-${System.getenv("GITHUB_RUN_NUMBER") ?: "dev"}"

subprojects { subproject ->
    apply(plugin: "java-library")
    apply(plugin: "maven-publish")

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    publishing {
        repositories {
            maven {
                url = "https://repo.grabsky.cloud/releases/"
                authentication { basic(BasicAuthentication) }
                credentials {
                    username = System.getenv("REPO_USERNAME")
                    password = System.getenv("REPO_SECRET")
                }
            }
            maven {
                url = "https://maven.pkg.github.com/Grabsky/configuration"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            maven(MavenPublication) { from components.java }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // JetBrains Annotations
        compileOnly("org.jetbrains:annotations:24.0.0")
        // Lombok
        compileOnly("org.projectlombok:lombok:1.18.26")
        annotationProcessor("org.projectlombok:lombok:1.18.26")
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
    }

}
