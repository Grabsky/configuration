plugins {
    id("java")
    id("maven-publish")
}

group = "cloud.grabsky"
version = "1.21.3-${System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"}"

subprojects { subproject ->
    apply(plugin: "java-library")
    apply(plugin: "maven-publish")

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21

    publishing {
        repositories {
            maven {
                url = "https://maven.pkg.github.com/Grabsky/configuration"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            maven(MavenPublication) { from components.java }
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // JetBrains Annotations
        compileOnly("org.jetbrains:annotations:26.0.1")
        // Lombok
        compileOnly("org.projectlombok:lombok:1.18.34")
        annotationProcessor("org.projectlombok:lombok:1.18.34")
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
    }

}
