plugins {
    id("java")
    id("maven-publish")
}

subprojects { subproject ->
    apply(plugin: "java-library")
    apply(plugin: "maven-publish")

    group = "cloud.grabsky"
    version = "0.9.7"

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17

    final Boolean isSnapshot = version.endsWith("-SNAPSHOT")
    final String hash7 = "git rev-parse --verify --short HEAD".execute().text.trim()

    publishing {
        repositories {
            maven {
                name 'Cloud'
                url (isSnapshot == true ? "https://repo.grabsky.cloud/snapshots" : "https://repo.grabsky.cloud/releases")
                credentials {
                    username = System.getenv("REPO_USERNAME")
                    password = System.getenv("REPO_SECRET")
                }
            }
        }
        publications {
            maven(MavenPublication) {
                version = (isSnapshot == true ? hash7 : version)
                from components.java
            }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        (options as CoreJavadocOptions).addStringOption("Xdoclint:none", "-quiet")
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // jetbrains annotations
        compileOnly("org.jetbrains:annotations:24.0.0")
        // lombok
        compileOnly("org.projectlombok:lombok:1.18.26")
        annotationProcessor("org.projectlombok:lombok:1.18.26")
    }

}
